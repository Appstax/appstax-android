apply plugin: 'com.android.library'

android {
    compileSdkVersion 19
    buildToolsVersion "22.0.1"
    defaultConfig {
        minSdkVersion 11
        targetSdkVersion 19
    }
    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
        }
    }
    lintOptions {
        disable 'InvalidPackage'
    }
}

dependencies {
    testCompile 'junit:junit:4.12'
    compile('com.appstax:appstax-java:1.0.1') {
        exclude group: 'org.json', module: 'json'
    }
}

// https://gist.github.com/kellyfj/9666950
android.libraryVariants.all { variant ->
    def name = variant.buildType.name

    if (name.equals(com.android.builder.core.BuilderConstants.DEBUG)) {
        return;
    }

    def task = project.tasks.create "jar${name.capitalize()}", Jar
    task.dependsOn variant.javaCompile
    task.from variant.javaCompile.destinationDir

    task.from configurations.compile.findAll {
        it.getName() != 'android.jar' && !it.getName().startsWith('junit') && !it.getName().startsWith('hamcrest')
    }.collect {
        it.isDirectory() ? it : zipTree(it)
    }

    artifacts.add('archives', task);
}